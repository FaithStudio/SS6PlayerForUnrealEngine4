//
// Copyright(C) Web Technology Corp.
//
// SpriteStudio6 Player for UE4 
//   オフスクリーンレンダリング用シェーダ 
//

#include "/Engine/Private/Common.ush"

Texture2D CellTexture;
SamplerState CellTextureSampler;
Texture2D MaskTexture;
SamplerState MaskTextureSampler;

struct VertexOut
{
	float4 Position : SV_POSITION;
	float4 Color : COLOR0;
	float2 TexCoord : TEXCOORD0;
	float2 ColorBlend : TEXCOORD1;	// [0]:ColorBlendType, [1]:ColorBlendRate
};

VertexOut MainVS(
	in float2 InPosition : ATTRIBUTE0,
	in float4 InColor : ATTRIBUTE1,
	in float2 InTexCoord : ATTRIBUTE2,
	in float2 InColorBlend : ATTRIBUTE3
	)
{
	VertexOut VOut;
	VOut.Position = float4(InPosition.xy, 0, 1);
	VOut.TexCoord = InTexCoord.xy;
	VOut.Color = InColor;
	VOut.ColorBlend = InColorBlend;
	return VOut;
}

float4 CalcColorPS(VertexOut In) : SV_Target0
{
	float4 TextureColor;
	TextureColor = Texture2DSample(CellTexture, CellTextureSampler, In.TexCoord);

	float ColorBlendRate = In.ColorBlend.y;
	float4 BlendColor[7];
	BlendColor[0] = (TextureColor * (1.0 - ColorBlendRate)) + (In.Color * ColorBlendRate);						// Mix(Whole)
	BlendColor[1] = (TextureColor * (1.0 - ColorBlendRate)) + (TextureColor * In.Color * ColorBlendRate);		// Mul
	BlendColor[2] = TextureColor + (In.Color * ColorBlendRate);													// Add
	BlendColor[3] = TextureColor - (In.Color * ColorBlendRate);													// Sub
	BlendColor[4] = TextureColor;																				// Invalid
	BlendColor[5] = BlendColor[1] * TextureColor.a;																// Effect
	BlendColor[6] = (TextureColor * (1.0 - In.Color.a))     + (In.Color * In.Color.a);							// Mix(Vertex) 

	uint ColorBlendType = uint(In.ColorBlend.x);
	float4 OutColor = BlendColor[ColorBlendType];
	OutColor.a = (6 != ColorBlendType) ? (TextureColor.a * In.Color.a) : (TextureColor.a);

	return OutColor;
}

float4 MainPS(VertexOut In) : SV_Target0
{
	return CalcColorPS(In);
}
float4 MainMaskedPS(VertexOut In) : SV_Target0
{
	float4 Color = CalcColorPS(In);
	int Mask = (1 + int(Texture2DSample(MaskTexture, MaskTextureSampler, In.Position).r * 255)) % 2;
	return float4(Color.rgb, Color.a * (float)Mask);
}
